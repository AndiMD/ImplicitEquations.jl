# ImplicitEquations

[![Build Status](https://travis-ci.org/jverzani/ImplicitEquations.jl.svg?branch=master)](https://travis-ci.org/jverzani/ImplicitEquations.jl)



This paper by
[Tupper](http://www.dgp.toronto.edu/people/mooncake/papers/SIGGRAPH2001_Tupper.pdf)
details a method for graphing two-dimensional implicit equations and
inequalities. This package gives an
implementation of the  paper's basic algorithms to allow
the `julia` user to naturally represent and easily render graphs of
implicit functions and equations.



For example, the
[Devils curve](http://www-groups.dcs.st-and.ac.uk/~history/Curves/Devils.html)
is graphed over the default region as follows:

```
using ImplicitEquations
using Winston 

a,b = -1,2
f(x,y) = y^4 - x^4 + a*y^2 + b*x^2
r = (f==0)
plot(r)
```

![DevilsCurve](http://i.imgur.com/LChTzC1.png)


The `f == 0` expression above creates a `Predicate` that is 
graphed by `plot`. For all but the case of `f==g` (for two functions) `Predicate`s are
generated by overloading the logical operators for functions on the
left and real values or functions on the right: `<`, `<=` (`\le<tab>`), `\Equal<tab>`, `\lessgtr<tab>`, `>=` (`\ge<tab>`), and `>` . As `==` and `!=` can already be
used to compare functions in a different way, we use either `eq(f,g)` or `neq(f,g)`
for these comparisons. Should infix notation be preferred, the Unicode
operator `f \Equal<tab> g` or `f \lessgtr<tab> g` may be used.

For example, the
[Trident of Newton](http://www-history.mcs.st-and.ac.uk/Curves/Trident.html)
can be represented in Cartesian form as follows:

```
## trident of Newton
c,d,e,h = 1,1,1,1
f(x,y) = x*y
g(x,y) =c*x^3 + d*x^2 + e*x + h
plot(eq(f,g)) ## aka f ⩵ g (using Unicode\Equal<tab>)
```

![Newton trident](http://i.imgur.com/7NxtUsK.png)


Inequalities can be graphed as well

```
f(x,y)= (y-5)*cos(4*sqrt((x-4)^2 + y^2))
g(x,y) = x*sin(2*sqrt(x^2 + y^2))
r = f < g
plot(r, -10, 10, -10, 10, W=2^9, H=2^9)
```

![Inequality](http://i.imgur.com/aEFjlTp.png)


The coloring scheme employed follows Tupper and is based on pixels:

* if a pixel is white, there are no solutions of the predicate `r` within the
pixel;
* if a pixel is black, there is at least one solution of the predicate `r`
within the pixel;
* if a pixel is red, there may or may not be solutions of the predicate `r`
within the pixel.

(In the above pixel does not refer to screen pixels for all the
graphical outputs, though is should as much as possible. Further, we
fudge a bit on the white case for single pixels where equality is of
interest -- it should be we can't find a solution after looking
randomly for a number of points.)


This graph illustrates the algorithm employed here:

```
f(x,y) = y - sqrt(x)
plot(f==0, W=2^4, H=2^4, offset=0)
```

![Algorithm](http://i.imgur.com/8Mtmb7v.png)

The basic algorithm is to initially break up the graphing region into
square regions. (This uses the number of pixels, which are specified
by `W` and `H` above.)  There regions are checked for the
predicate. If definitely not, the region is painted white; if
definitely yes, the region is painted black; else the square region is
subdivided into 4 smaller regions and the above is repeated until
subdivision would be below the pixel level. At which point, the
remaining 1-by-1 pixels are checked for possible solutions, for
example for equalities where continuity is known a random sample of
points is investigated with the intermediate value theorem.


This example, the
[Batman equation](http://yangkidudel.wordpress.com/2011/08/02/love-and-mathematics/),
Uses a few new things: the `screen` function is used to restrict
ranges and logical operators to combine predicates.

```
f0(x,y) = ((x/7)^2 + (y/3)^2 - 1)  *   screen(abs(x)>3) * screen(y > -3*sqrt(33)/7) 
f1(x,y) = ( abs(x/2)-(3 * sqrt(33)-7) * x^2/112 -3 +sqrt(1-(abs((abs(x)-2))-1)^2)-y)
f2(x,y) = y - (9 - 8*abs(x))       *   screen((abs(x)>= 3/4) &  (abs(x) <= 1) )
f3(x,y) = y - (3*abs(x) + 3/4)     *   I_((1/2 < abs(x)) & (abs(x) < 3/4))    # alternate name for screen
f4(x,y) = y - 2.25                 *   I_(abs(x) <= 1/2) 
f5(x,y) = (6 * sqrt(10)/7 + (1.5-.5 * abs(x)) - 6 * sqrt(10)/14 * sqrt(4-(abs(x)-1)^2) -y) * screen(abs(x) >= 1)

r = (f0==0) | (f1==0) | (f2== 0) | (f3==0) | (f4==0) | (f5==0)
plot(r, -7, 7, -4, 4)
```

![Batman Curve](http://i.imgur.com/Buyd9Fb.png)

The above example illustrates a few things:

* predicates can be joined logically with `&`, `|`. Use `!` for negation.

* The `screen` function can be used to restrict values according to
  some predicate call.

* the logical comparisons such as `(abs(x)>= 3/4) & (abs(x) <= 1)`
  within `screen` are not typical in that one can't write `3/4 <=
  abs(x) <= 1`, a convenient `Julian` syntax. This is due to the fact that the "`x`s"
  being evaluated are not numbers, rather intervals via
  `ValidatedNumerics`. For intervals, values may be true, false or
  "maybe" so a different interpretation of the logical operators is
  given that doesn't lend itself to the more convenient notation.

* rendering can be slow. There are two reasons: images that require a
  lot of checking, such as the inequality above, are slow just because
  more regions must be analyzed. As well, some operations are slow,
  such as division, as adjustments for discontinuities are slow. (And
  by slow, it can means really slow. The difference between rendering
  `(1-x^2)*(2-y^2)` and `csc(1-x^2)*cot(2-y^2)` can be 10 times.)

## A "typical" application

A common calculus problem is to find the tangent line using implicit
differentiation. The difficulty here is adding a layer to a graph
produced by `plot(r::Predicate)`. How to do this varies with the
underlying plotting package. With the `Winston` graph, the `wgraph`
function returns a `FramedPlot` object which can have a curve added to
it. This illustrates:

```
f(x,y) = x^2 + y^2
p = wgraph(f == 2*3^2)       # this is Winston specific
a,b = 3,3
dydx(a,b) = -b/a             # implicit differentiate to get dy/dx =-y/x
tl(x) = b + dydx(a,b)*(x-a)  
xs = linspace(-5, 5)
add(p, Curve(xs, map(tl,xs))) # add to a Winston plot
```

A more general alternative would be to use another function to represent
the tangent line and use an or operation to plot both graphs:

```
g(x,y) = y - tl(x)
r = (f == 2*3^2) | (g == 0)
plot(r)
```

## Display

The graphs can be rendered in different ways.

* The `asciigraph` function is always available and makes a simple text-based plot.
* When one of several plotting packages are loaded, their `plot` function is extended to display a graph when the first argument is a `Predicate`, as generated by the expressions above. For each package a separate plotting function is exported:
- for `Winston`, the function  `wgraph` does the work,
- for `PyPlot`, the function  `pgraph` does the work, and
- for `Gadfly`, the function  `ggraph` does the work,
* For rendering with `Cairo` and `Gtk`, a function `cgraph` is exported, but no `plot` method is introduced
* For rendering with `Patchwork`, the `pwgraph` function is exported, but no plot method is introduced

  The SVG-based solutions, useful in `IJulia`, are `Patchwork` and
  `Gadfly`. These do not work as advertised above -- there is some
  issue with the load hook provided by `Jewel.@require`. To use them
  requires loading the code separately. The `Cairo` hook also has
  problems due to `Tk` and `Gtk` not playing nicely, so it too is
  disabled for now.


## TODO

*LOTS*:

* Check out these graphs to see which can be done
- http://www.xamuel.com/graphs-of-implicit-equations/
- http://www.peda.com/grafeq/gallery.html

* branch cut tracking and interval sets are employed by Tupper, these
  could be added. This would allow some other functions such as `mod`,
  or `±` to be defined.

* Tupper sketches out how to be more rigorous with computing whether a region is black or white.

* increase speed (could color 1-pixel regions better if so, perhaps; division checks; type stability).

